float sdCappedCylinder(vec3 p, float h, float r) 
{
    vec2 d = abs(vec2(length(p.xz), p.y)) - vec2(r, h);
    return min(max(d.x, d.y), 0.0) + length(max(d, 0.0));
}

float sdCappedTorus(vec3 p, vec2 sc, float ra, float rb)
{
    p.x = abs(p.x);
    float k = (sc.y*p.x>sc.x*p.y) ? dot(p.xy,sc) : length(p.xy);
    return sqrt(dot(p,p) + ra*ra - 2.0*ra*k) - rb;
}

float sdBaston(vec3 p)
{
    // Animation rotation
    float t = iTime * 0.7;
    float cs = cos(t), sn = sin(t);
    p.xz = mat2(cs, -sn, sn, cs) * p.xz;

    float torusHeight = 0.5;
    float torusRadius = 0.4;
    float torusThickness = 0.12;

    // ---- Torus ----
    vec3 pt = p - vec3(0.0, torusHeight, 0.0);
    float head = sdCappedTorus(pt, vec2(0.955, 0.3), torusRadius, torusThickness);

    // ---- Cylinder attached to side of torus ----
    float attachX = torusRadius + torusThickness; // position at rim
    vec3 ph = p - vec3(attachX, torusHeight, 0.0);
    float handle = sdCappedCylinder(ph, 1.4, 0.15);

    // ---- Smooth Union ----
    float k = 0.20;
    float h = clamp(0.5 + 0.5 * (handle - head) / k, 0.0, 1.0);
    return mix(handle, head, h) - k * h * (1.0 - h);
}



float mapScene(vec3 p)
{
    float tilt = radians(20.0);
    float cx = cos(tilt), sx = sin(tilt);
    p.yz = mat2(cx, -sx, sx, cx) * p.yz;

    return sdBaston(p);
}
// -------- Lighting --------
vec3 getNormal(vec3 p)
{
    const float e = 0.001;
    return normalize(vec3(
        mapScene(p + vec3(e,0,0)) - mapScene(p - vec3(e,0,0)),
        mapScene(p + vec3(0,e,0)) - mapScene(p - vec3(0,e,0)),
        mapScene(p + vec3(0,0,e)) - mapScene(p - vec3(0,0,e))
    ));
}

// -------- Raymarch --------
float raymarch(vec3 ro, vec3 rd)
{
    float t = 0.0;
    for(int i=0;i<120;i++)
    {
        vec3 p = ro + rd * t;
        float d = mapScene(p);
        if(d < 0.001) return t;
        t += max(d,0.001);
        if(t > 20.0) break;
    }
    return -1.0;
}


// -------- Main --------
void mainImage(out vec4 fragColor, in vec2 fragCoord)
{
    vec2 uv = (fragCoord - 0.5 * iResolution.xy) / iResolution.y;
    
    // Camera setup
    vec3 ro = vec3(0.0, 0.9, 6.0);
    vec3 lookAt = vec3(0.0, 1.2, 0.0);
    vec3 forward = normalize(lookAt - ro);
    vec3 right = normalize(cross(vec3(0.0,1.0,0.0), forward));
    vec3 up = cross(forward, right);
    vec3 rd = normalize(uv.x*right + uv.y*up + 1.0*forward);

    float tHit=raymarch(ro,rd);
    float t = iTime * 0.7;
    float cs = cos(t), sn = sin(t);
    

    if(tHit > 0.0)
    {
        vec3 lightPos = vec3(2.0, 3.0, 2.0);
        vec3 p = ro + rd * tHit;
        vec3 n = getNormal(p);
        vec3 l = normalize(lightPos - p);

        //ambient
        vec3 ambient = vec3(0.1,0.18,0.3);
        
        //diffuse
        vec3 layer1 = vec3(0.4, 0.7, 1.0);
        vec3 layer2 = vec3(0.3, 0.5, 0.8);
        vec3 layer3 = vec3(0.2, 0.4, 0.6);
        vec3 layer4 = vec3(0.1, 0.3, 0.5);
        vec3 layer5 = vec3(0.0, 0.2, 0.4);
        
        
        
        float diff=max(dot(n,l),0.0);
        vec3 diffuseColor = layer1 * diff * 0.2
                  + layer2 * diff * 0.2
                  + layer3 * diff * 0.2
                  + layer4 * diff * 0.2
                  + layer5 * diff * 0.2;
        vec3 diffuse=diffuseColor*diff*2.0;
        
        
        //specular
        float specMultiplier = pow(max(dot(reflect(-l,n),-rd),0.0),64.0)*1.5;
        vec3 specColor=vec3(1.0)*specMultiplier;
        
      
        vec3 col = ambient + diffuse  + specColor;
        fragColor = vec4(col, 1.0);

    }
    else
    {
        fragColor = vec4(0.02, 0.02, 0.03, 1.0);
    }
}